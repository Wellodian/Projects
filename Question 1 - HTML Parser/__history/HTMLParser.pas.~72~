unit HTMLParser;

interface

uses
  Classes, SysUtils, Dialogs;

type
    THTMLParser = class
        private
            FResultText: string;
            FHTMLText: string;
            procedure Parse(sHTMLText: string; slOpenTags: TStringList);
            function IsClosingTag(tagPosition: integer): boolean;
            function IsOpeningTag(tagPosition: integer): boolean;
        public
            constructor Create(text: string);
            procedure ParseText;
            property ResultText: string read FResultText;
    end;

implementation

constructor THTMLParser.Create(text: string);
begin
    inherited Create;
    FHTMLText := text;
end;

procedure THTMLParser.ParseText;
var
    slOpenTags: TStringList;
begin
    slOpenTags := TStringList.Create;
    try
        Parse(FHTMLText, slOpenTags);
    finally
        slOpenTags.Free;
    end;
end;

function THTMLParser.IsClosingTag(tagPosition: integer): boolean;
begin
    Result := (FHTMLText[tagPosition] = '<')
        and (FHTMLText[tagPosition + 1] = '/')
        and (FHTMLText[tagPosition + 2] in ['A'..'Z'])
        and (FHTMLText[tagPosition + 3] = '>');
end;

function THTMLParser.IsOpeningTag(tagPosition: integer): boolean;
begin
    Result := (FHTMLText[tagPosition] = '<')
        and (FHTMLText[tagPosition + 1] in ['A'..'Z'])
        and (FHTMLText[tagPosition + 2] = '>');
end;

procedure THTMLParser.Parse(sHTMLText: string; slOpenTags: TStringList);
// Recursive procedure to determine if a string is well formed,
// based on the rules provided in the question.
var
    iPos: integer;
const
    CORRECTLY_TAGGED = 'Correctly tagged paragraph';
    UNEXPECTED_CLOSING_TAG = 'Expected # found </%s>';
    INCORRECT_CLOSING_TAG = 'Expected </%s> found </%s>';
    MISSING_CLOSING_TAG = 'Expected </%s> found #';
begin
    // find position of first potential HTML tag in text param
    iPos := Pos('<', sHTMLText);

    if (iPos > 0) then
    begin
        // check if closing tag
        if (IsClosingTag(iPos)) then
        begin
            // if no open tags are outstanding, then it's an unexpected closing tag
            if (slOpenTags.Count = 0) then
            begin
                FResultText := Format(UNEXPECTED_CLOSING_TAG, [sHTMLText[iPos + 2]]);
                Exit;
            end
            else
            begin
                // if open tags are waiting for a matching closing tag, then check for a match
                // if matched, then remove matching opening tag from slOpenTags string list
                // otherwise, exit with result text that the incorrect closing tag was encountered.
                if (SameText(slOpenTags.Strings[slOpenTags.Count-1], sHTMLText[iPos + 2])) then
                begin
                    slOpenTags.Delete(slOpenTags.Count-1);
                    FHTMLText := Copy(sHTMLText, iPos + 4, Length(sHTMLText) - (iPos + 3));
                    Parse(FHTMLText, slOpenTags);
                end
                else
                begin
                    FResultText := Format(INCORRECT_CLOSING_TAG, [slOpenTags.Strings[slOpenTags.Count-1], sHTMLText[iPos + 2]]);
                    Exit;
                end;
            end;
        end
        // if opening tag, then add to open tags string list
        // otherwise, move forward in the HTML text and recursively Parse the text
        else if (IsOpeningTag(iPos)) then
        begin
            slOpenTags.Add(sHTMLText[iPos + 1]);
            FHTMLText := Copy(sHTMLText, iPos + 3, Length(sHTMLText) - (iPos + 2));
            Parse(FHTMLText, slOpenTags);
        end
        else
        begin
            FHTMLText := Copy(sHTMLText, iPos + 1, Length(sHTMLText) - (iPos));
            Parse(FHTMLText, slOpenTags);
        end;
    end
    else
    begin
        if (slOpenTags.Count = 0) then
            FResultText := CORRECTLY_TAGGED
        else
            FResultText := Format(MISSING_CLOSING_TAG, [slOpenTags.Strings[slOpenTags.Count-1]]);
    end;
end;

end.
